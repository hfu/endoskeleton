hidden const param {
  glyphs = "https://gsi-cyberjapan.github.io/optimal_bvmap/glyphs/{fontstack}/{range}.pbf"
  sprite = "https://gsi-cyberjapan.github.io/optimal_bvmap/sprite/std"
  pmtiles = "pmtiles://https://cyberjapandata.gsi.go.jp/xyz/" + 
    "optimal_bvmap-v1/optimal_bvmap-v1.pmtiles"
  font = "NotoSansJP-Regular"
}

hidden const color {
  raspberry = "#E30B5D"
}

open class Layer {
  hidden sl: String?
  source: String?
  id: String? = sl
  `source-layer`: String? = sl
  filter: Listing?
  type: String?
  maxzoom: Int?
  minzoom: Int?
  paint {}
  layout {}
}

class Background extends Layer {
  type = "background"
  id = "background"
  hidden c = "#ff0"
  paint { `background-color` = c }
}

class Line extends Layer {
  source = "v"
  type = "line"
  hidden c = color.raspberry
  /* hidden z = new { 10 0.2 18 1 } */
  paint {
    /* `line-width` = new {
      "interpolate"
      new { "linear" }
      new { "zoom" }
      z[0]
      z[1]
      z[2]
      z[3]
    } */
    `line-color` = c
  }
}

class Fill extends Layer {
  source = "v"
  type = "fill"
  hidden c = color.raspberry
  /* hidden z = new { 15 0 20 1 } */
  paint {
    /* `fill-opacity` = new {
      "interpolate"
      new { "linear" }
      new { "zoom" }
      z[0]
      z[1]
      z[2]
      z[3]
    } */
    `fill-color` = c
  }
}

class Symbol extends Layer {
  source = "v"
  type = "symbol"
  hidden c = color.raspberry
  layout {
    `text-font` = new { param.font }
  }
  paint {
    `text-color` = color.raspberry
  }
}

version = 8
glyphs = param.glyphs
sprite = param.sprite
sources {
  v { type = "vector" url = param.pmtiles }
}
layers = new {
  new Background { c = "#f8f9fa" }
  new Fill { sl = "AdmArea" c = "rgb(255, 255, 0, 0.2)" }
  new Fill { sl = "WA" }
  new Fill { sl = "TpgphArea" }
  new Line { sl = "Cstline" }
  new Line { sl = "RdCL" }
  new Line { sl = "RvrCL" }
  new Line { sl = "WL" }
  new Line { sl = "WRltLine" }
  new Line { sl = "AdmBdry" }
  new Line { sl = "Cntr" }
  new Symbol {
    sl = "Cntr"
    id = "Cntr_Numeric"
    filter = new {
      "=="
      new { "get" "vt_code" }
      7352
    }
    layout {
      `text-field` = new { "get" "vt_alti" }
      `text-allow-overlap` = true
      `text-ignore-placement` = true
      `symbol-placement` = "line-center"
      `text-pitch-alignment` = "viewport"
      `text-rotation-alignment` = "map"
      `text-size` = 10
      `text-max-angle` = 360
    }
  }
}
