hidden const param {
  glyphs = "https://gsi-cyberjapan.github.io/optimal_bvmap/glyphs/{fontstack}/{range}.pbf"
  sprite = "https://gsi-cyberjapan.github.io/optimal_bvmap/sprite/std"
  pmtiles = "pmtiles://https://cyberjapandata.gsi.go.jp/xyz/" + 
    "optimal_bvmap-v1/optimal_bvmap-v1.pmtiles"
  font = "NotoSansJP-Regular"
}

hidden const lib {
  opac = 1
  trans = 0.2
  zoom = new {
    "interpolate"
    new { "linear" }
    new { "zoom" }
    4 trans
    4.5 opac
    5 trans
    5.5 opac
    6 trans
    6.5 opac
    7.5 opac
    8 trans
    8.5 opac
    9 trans
    9.5 opac
    10 trans
    10.5 opac
    11 trans
    11.5 opac
    12.5 opac
    13 trans
    13.5 opac
    14 trans
    14.5 opac
    15 trans
    15.5 opac
    16 trans
    16.5 opac
  }
}

hidden const color {
  raspberry = "#e30b5d"
  white = "#f2f2f2"
  lightGray = "#e6e6e6"
  aqua = "#cddcfc"
  // See https://unopengis.github.io/smartmaps/about/brand
  smartBlue = "#5c81b8"
  collaborationGreen = "#97be84"
  innovationRed = "#97be84"
  // UN
  processCyan = "#00aeef"
}

open class Layer {
  hidden sl: String?
  source: String? id: String? = sl `source-layer`: String? = sl
  filter: Listing? type: String? maxzoom: Int? minzoom: Int?
  paint {} layout {}
}

class Background extends Layer {
  type = "background" id = "background"
  hidden c = color.white
  paint { `background-color` = c }
}

class Line extends Layer {
  source = "v" type = "line"
  hidden c = color.raspberry
  paint {
    `line-opacity` = lib.zoom
    `line-color` = c
  }
}

class Fill extends Layer {
  source = "v" type = "fill"
  hidden c = color.raspberry
  paint {
    `fill-opacity` = lib.zoom
    `fill-color` = c
  }
}

open class Symbol extends Layer {
  source = "v" type = "symbol"
  hidden c = color.raspberry
  layout {
    `text-font` = new { param.font }
  }
  paint {
    `text-color` = color.raspberry
    `text-opacity` = lib.zoom
  }
}

class Annotation extends Symbol {
  layout {
    `text-field` = new { "get" "vt_text" }
    `text-size` = 10
  }
  paint {
    `text-color` = color.raspberry
    `text-halo-color` = color.white
    `text-halo-width` = 2
    `text-halo-blur` = 1
  }
}

class LineSymbol extends Symbol {
  hidden code: Int? hidden field: String?
  filter = new { "==" new { "get" "vt_code" } code }
  layout {
    `text-field` = new { "get" field }
    `symbol-placement` = "line-center"
    `text-pitch-alignment` = "viewport"
    `text-rotation-alignment` = "map"
    `text-size` = 10
    `text-max-angle` = 360
  }
}

version = 8 glyphs = param.glyphs sprite = param.sprite
name = "endoskeleton"
sources { v { type = "vector" url = param.pmtiles } }
layers = new {
  new Background { }
  new Fill { sl = "AdmArea" c = color.lightGray }
  new Fill { sl = "WA" c = color.aqua }
  new Fill { sl = "TpgphArea" }
  new Line { sl = "Cstline" }
  new Line { sl = "RvrCL" }
  new Line { sl = "WL" }
  new Line { sl = "WRltLine" }
  new Line { sl = "AdmBdry" }
  new Line { sl = "Cntr" }
  new LineSymbol { sl = "Cntr" id = "Cntr_Numeric" code = 7352 field = "vt_alti" }
  new Line { sl = "Isbt" }
  new LineSymbol { sl = "Isbt" id = "Isbt_Numeric" code = 7372 field = "vt_depth" }
  new Line { sl = "TpgphLine" }
  new Fill { sl = "WStrA" }
  new Line { sl = "WStrL" }
  new Line { sl = "RdCL" }
  new Line { sl = "RailCL" }
  new Fill { sl = "BldA" }
  new Fill { sl = "UndergroundStation" }
  new Line { sl = "RdEdg" }
  new Line { sl = "RdCompt" }
  new Line { sl = "RailTrCL" }
  new Line { sl = "SpcfArea" }
  new Fill { sl = "StrctArea" }
  new Line { sl = "StrctLine" }
  new Line { sl = "TrfTnnlEnt" }
  new Line { sl = "PwrTrnsmL" }
  new Symbol { sl = "Anno" id = "Anno_Symbol"
    layout {
      `icon-allow-overlap` = true
      `icon-image` = new { "step" new { "zoom" }
        new { "match" new { "get" "vt_code" } new { 1301 1302 1303 }
	"人口50万人未満-500" "" } 6 new { "match"
	new { "get" "vt_code" } 1301 "人口100万人以上-500" 1302 "人口50万-100万人未満-500" 
	1303 "人口50万人未満-500" 6368 "主要な港-500" 6376 "主要な空港-500"
	7201 "標高点（測点）-20" "" } 8 new { "match" new { "get" "vt_code" }
	1401 "都道府県所在地-100" 1402 "市役所・東京都の区役所（都道府県所在都市以外）-20"
	1403 "町・村-20" 2901 "国道番号-20" 2903 "都市高速道路番号-20"
	2904 "高速道路番号-20" 2941 "インターチェンジ-20" 2942 "ジャンクション-20"
	2945 "スマートインターチェンジ-20" 3221 "灯台-20" 6351 "採鉱地-20"
	6367 "特定重要港-20" 6368 "重要港-20" 6375 "国際空港-20" 
	6376 "国際空港以外の拠点空港等-20" 7102 "標高点（測点）-20"
	7201 "標高点（測点）-20" 7221 "火山-20" "" } 11 new { "match" 
	new { "get" "vt_code" } 621 "都道府県所在地-20" 631 "高等学校・中等教育学校-20"
	632 "高等学校・中等教育学校-20" 633 "高等学校・中等教育学校-20"
	653 "発電所等-20" 661 "神社-20" 662 "寺院-20" 2901 "国道番号-20"
	2903 "都市高速道路番号-20" 2904 "高速道路番号-20" 2941 "インターチェンジ-20"
	2942 "ジャンクション-20" 2945 "スマートインターチェンジ-20"
	3205 "市役所・東京都の区役所-20" 3206 "町村役場・政令指定都市の区役所-20"
	3212 "高等学校・中等教育学校-20" 3218 "郵便局-20" 3221 "灯台-20"
	3231 "神社-20" 3232 "寺院-20" 3241 "警察署-20" 3243 "病院-20" 3261 "工場-20"
	4102 "風車-20" 4103 "油井・ガス井-20" 6311 "田-20" 6312 "畑-20" 6313 "畑-20"
	6314 "畑-20" 6321 "広葉樹林" 6322 "針葉樹林-20" 6331 "温泉-20" 
	6332 "噴火口・噴気口-20" 6341 "史跡・名勝・天然記念物-20" 6342 "城跡-20"
	6351 "採鉱地-20" 6361 "港湾-20" 6362 "漁港-20" 6367 "特定重要港-20"
	6371 "国際空港-20" 6373 "自衛隊等の飛行場-20" 6375 "国際空港-20"
	6381 "自衛隊-20" 7101 "電子基準点-20" 7102 "三角点-20" 7201 "標高点（測点）-20"
	8103 "発電所等-20" "" } 14 new { "match" new { "get" "vt_code" }
	2901 "国道番号-20" 3201 "官公署" 3202 "裁判所" 3203 "税務署" 3204 "外国公館"
	3205 "市役所・東京都の区役所" 3206 "町村役場・政令指定都市の区役所"
	3211 "交番" 3212 "高等学校・中等教育学校" 3213 "小学校" 3214 "小学校"
	3215 "老人ホーム" 3216 "博物館法の登録博物館・博物館相当施設" 3217 "図書館"
	3218 "郵便局" 3221 "灯台" 3231 "神社" 3232 "寺院" 3241 "警察署" 3242 "消防署"
	3243 "病院" 3244 "保健所" 4101 "煙突" 4102 "風車" 4103 "油井・ガス井"
	4104 "記念碑" 4105 "自然災害伝承碑" 5801 "滝" 6301 "墓地" 6311 "田" 6312 "畑"
	6313 "茶畑" 6314 "果樹園" 6321 "広葉樹林" 6322 "針葉樹林" 6323 "竹林"
	6324 "ヤシ科樹林" 6325 "ハイマツ地" 6326 "笹地" 6327 "荒地" 6331 "温泉"
	6332 "噴火口・噴気口" 6341 "史跡・名勝・天然記念物" 6342 "城跡" 6351 "採鉱地"
	6361 "港湾" 6362 "漁港" 7101 "電子基準点" 7102 "三角点" 7103 "水準点"
	7201 "標高点（測点）" 7601 "砂礫地（領域が不明瞭な場合）-20"
	7621 "雨裂（下部）-20" 7701 "水面標高-20" 7711 "水深-20" 8103 "発電所等"
	8105 "電波塔" "" } 17 new { "match" new { "get" "vt_code" }
	2901 "国道番号-20" 4105 "自然災害伝承碑" 7101 "電子基準点" "" } }
	`icon-size` = 0.5
      }
      paint {
	`icon-opacity` = lib.zoom
      }
  }
  new Annotation { sl = "Anno" id = "Anno_Annotation" }
}
